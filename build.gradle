apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

def localMavenRepo = 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath
repositories {
	mavenRepo url: localMavenRepo
	mavenCentral()
}

version = '1.0-SNAPSHOT'
group = 'com.greenmoonsoftware'
install.dependsOn ':build'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

dependencies {
	groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.6'
	compile group: 'org.codehaus.groovy.modules.http-builder', name: 'http-builder', version: '0.5.2'
	testCompile group: 'junit', name: 'junit', version: '4.8.2'
}

artifacts {
	archives jar
	archives javadocJar
	archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
              authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
               name 'green-tea-teast'
               packaging 'jar'
               description 'A Groovy framework for functional testing rest services. Intended to help you relax while you REST.'
               url 'https://github.com/greathouse/green-tea-test'

               scm {
                   url 'scm:git@github.com:greathouse/green-tea-test.git'
                   connection 'scm:git@github.com:greathouse/green-tea-test.git'
                   developerConnection 'scm:git@github.com:greathouse/green-tea-test.git'
               }

               licenses {
                   license {
                       name 'The Apache Software License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                       distribution 'repo'
                   }
               }

               developers {
                   developer {
                       id 'greathouse'
                       name 'Robert G. Greathouse'
                   }
               }
           }
        }
    }
}